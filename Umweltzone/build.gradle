buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.3'
        classpath 'net.sf.proguard:proguard-gradle:4.10'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 4 // bump for dogfood builds, public betas, etc.

def gitSha() {
    return 'git rev-parse HEAD'.execute().text.trim()
}

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

Properties props = new Properties()
props.load(new FileInputStream(file("gradle.properties")))

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfig """\
          public static final String BUILD_VERSION = "${versionName}";
          public static final int BUILD_VERSION_CODE = ${versionCode};
          public static final String BUILD_TIME = "${buildTime()}";
          public static final String GIT_SHA = "${gitSha()}";
        """
    }

    signingConfigs {
        release {
            storeFile file(props['signing.release.keystoreFilePath'])
            storePassword props['signing.release.keystorePassword']
            keyAlias props['signing.release.keyAlias']
            keyPassword props['signing.release.keyPassword']
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            zipAlign = true
        }
        release {
            runProguard true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.0.0'
    compile 'com.android.support:support-v4:19.0.0'
    compile 'com.google.android.gms:play-services:3.2.+'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.+'
    compile fileTree(dir: 'libs', include: '*.jar')
}
